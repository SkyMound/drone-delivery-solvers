package drone_delivery.solver

import io.sarl.api.core.OpenEventSpace
import io.sarl.lang.core.AgentContext
import io.sarl.lang.core.Event
import io.sarl.lang.core.EventListener
import io.sarl.lang.core.SRE
import io.sarl.lang.core.SREBootstrap
import java.util.List
import java.util.UUID
import java.util.concurrent.ConcurrentHashMap
import org.arakhne.afc.math.geometry.d2.d.Vector2d
import drone_delivery.solver.gui.EnvironmentGui
import java.util.Comparator
import java.util.Collections
import org.arakhne.afc.math.geometry.d2.Vector2D
import java.util.ArrayList
import io.sarl.api.core.Destroy
import java.lang.reflect.Parameter

class DroneSimulation implements EventListener {
	public static val id = UUID::randomUUID

	/** 
	 * SRE Kernel instance
	 */
	var kernel : SREBootstrap

	/** 
	 * The default SARL context where environment and drones are spawned
	 */
	var defaultSARLContext : AgentContext


	var minDataX : double
	var minDataY : double
	
	/** 
	 * Identifier of the environment
	 */
	var environment : UUID

	var width = Settings::EnvtWidth

	var height = Settings::EnvtHeight
	var guiwidth = Settings::EnvtWidth

	var guiheight = Settings::EnvtHeight

	/** 
	 * Map buffering drones before launch/start
	 */
	var dronesToLaunch : int
	
	var nbParcelinSim : int
	
	var parcelToCreate : List<Parcel>
	
	
	/** 
	 * Map buffering drones' bodies before launch/start
	 */
	var droneBodies : ConcurrentHashMap<UUID, PerceivedDroneBody>

	var dronesCount : int

	var depotPos : Vector2d
	
	var housesPos : List<Vector2d>
	
	var deliveriesTime : List<Integer>

	/** 
	 * Boolean specifying id the simulation is started or not.
	 */
	var isSimulationStarted = false

	/** 
	 * the vent space used to establish communication between GUI and agents,
	 * Especially enabling GUI to forward start event to the environment, 
	 * respectively the environment to send GUIRepain at each simulation step to the GUI
	 */
	var ^space : OpenEventSpace

	/** 
	 * The Graphical user interface
	 */
	var myGUI : EnvironmentGui

	new (nbDrones : int, nbParcel : int, cityfilePath : String, parcelfilePath : String) {
		dronesCount = 0

		this.droneBodies = new ConcurrentHashMap

		dronesToLaunch = nbDrones
		nbParcelinSim = nbParcel
		housesPos = createHousePosList(cityfilePath).toList
		parcelToCreate = createParcelsList(cityfilePath, parcelfilePath)
		var newNnvBoudaries = resizeEnvToModel
		translatehousesPosTo0(minDataX, minDataY)
		translateParcelsPosTo0(minDataX, minDataY)

		width = (newNnvBoudaries.get(0) * 1.2).toInteger
		height = (newNnvBoudaries.get(1) * 1.2).toInteger
		deliveriesTime = newArrayList
		
	}
	
	def start : void {
		launchAllAgents
		isSimulationStarted = true
	}

	def stop : void {
		killAllAgent
		isSimulationStarted = false
	}
	def finished : boolean{
		while(isSimulationStarted){
			
		}
		return true
	}

	private def launchAllAgents : void {

		kernel = SRE::getBootstrap

		defaultSARLContext = kernel.startWithoutAgent

		environment = UUID::randomUUID

		kernel.startAgentWithID(typeof(Environment), environment, height, width)

		launchAllDrones
		
		launchDepot
		
		^space = defaultSARLContext.defaultSpace as OpenEventSpace

		var guidepotPos = new Vector2d(this.depotPos.x * guiwidth / width, depotPos.y * guiheight / height)
		this.myGUI = new EnvironmentGui(^space, guiheight, guiwidth, scaleDronePosToGui(this.droneBodies), guidepotPos,
			scaleHousesPosForGui(this.housesPos))

		// Registering to receive GUIRepaint events
		^space.registerWeakParticipant(this)
		// Sending start to Environment
		^space.emit(id, new Start(this.droneBodies))
	}
	
	private def resizeEnvToModel() {
		
		var minValueX = housesPos.get(0).x
		var minValueY = housesPos.get(0).y

		var maxValueX = housesPos.get(0).x
		var maxValueY = housesPos.get(0).y

		for (value : housesPos) {
			if ( value.x < minValueX) {
				minValueX = value.x

			}
			if ( value.y < minValueY) {
				minValueY = value.y

			}
			if ( value.x > maxValueX) {
				maxValueX = value.x

			}
			if ( value.y > maxValueY) {
				maxValueY = value.y

			}
		}
		minDataX = minValueX
		minDataY = minValueY		
		
		var newboudaries = newArrayList
		newboudaries.add(maxValueX - minValueX)
		newboudaries.add(maxValueY - minValueY)
		
		return newboudaries
	}
	private def translatehousesPosTo0(minX : double, minY : double){
		var newHousesPos = new ArrayList<Vector2d>
		for (house : housesPos) {
			newHousesPos.add(new Vector2d(house.x - minX, house.y - minY))
		}
		housesPos = newHousesPos
	}
	private def translateParcelsPosTo0(minX : double, minY : double) {
		var newParcelsToCreate = new ArrayList<Parcel>
		for (p : parcelToCreate) {
			newParcelsToCreate.add(new Parcel(p, new Vector2d(p.housePos.x - minX, p.housePos.y - minY)))
		}
		parcelToCreate = newParcelsToCreate
	}
	
	
	private def launchDepot : void {
		var initialPosition = new Vector2d(Settings::DepotPos.x - minDataX, Settings::DepotPos.y - minDataY)
		this.depotPos = initialPosition
		var de = UUID::randomUUID
		kernel.startAgentWithID(typeof(Depot), de, environment, initialPosition, "Depot", this.parcelToCreate,this.droneBodies)

		if (Settings::isLogActivated) {
			System.out.println("Lancement du dépot à la position " + initialPosition)
		}
	}
	
	private def launchAllDrones : void {
		for( var i =0; i< dronesToLaunch;i++){
			launchDrone("Drone"+i)
		}
	}
	
	private def launchDrone(droneName:String){
		var initialPosition = new Vector2d(Settings::DepotPos.x - minDataX, Settings::DepotPos.y - minDataY)
		var initSpeed = new Vector2d
		var objectiv = Objectiv.Charge
		var targetPos = null
		var battery =100.0f
		var d = UUID::randomUUID
		var weight = 4 //kg
		kernel.startAgentWithID(typeof(Drone), d, environment, initialPosition, initSpeed, objectiv, targetPos, battery,
			droneName)
		this.droneBodies.put(d, new PerceivedDroneBody(d, initialPosition, initSpeed, objectiv, targetPos, battery,weight))
		
		
		if (Settings::isLogActivated) {
			System.out.println("Création d'un drone à la position " + initialPosition )
		}
	}

	private def killAllAgent : void {
		// do something if you want to
		^space.emit(UUID.randomUUID, new Die)
		this.myGUI.dispose
	}
	
	
	def createParcelsList(cityfilePath : String, parcelfilePath : String) {
		var parcelList = CustomCSVReader.getparcelsFromCSV(cityfilePath, parcelfilePath, nbParcelinSim)
		
		Collections.sort(parcelList, new Comparator<Parcel>() {
		    @Override
			def compare(p1 : Parcel, p2 : Parcel) : int {
		        return p1.ordertime.compareTo(p2.ordertime)		}
		});
		return parcelList
	}
	
	def createHousePosList(cityfilePath : String) {
		return CustomCSVReader.getHousesFromCSV(cityfilePath).values
	}
	
	def scaleDronePosToGui(drones : ConcurrentHashMap<UUID, PerceivedDroneBody>){
		var guiPosDrones = new ConcurrentHashMap<UUID, PerceivedDroneBody>
		for (droneSet : drones.entrySet){
			var droneBody = droneSet.value
			var droneGuiPos = new Vector2d(droneBody.position.x * guiwidth / width,
				droneBody.position.y * guiheight / height)
			var guiPerceivedDrone = new PerceivedDroneBody(droneBody.owner, droneGuiPos, droneBody.vitesse,
				droneBody.objectiv, droneBody.targetPos, droneBody.battery, droneBody.weight)
			
			guiPosDrones.put(droneSet.key, guiPerceivedDrone)
		}
		return guiPosDrones
	}
	
	
	def scaleHousesPosForGui(housesPosenv : List<Vector2d>) {
		var newhp = new ArrayList<Vector2d>
		for(hp : housesPosenv){
			newhp.add(new Vector2d(hp.x * guiwidth / width, hp.y * guiheight / height))
		}
		return newhp
	}
	
	override getID : UUID {
		id
	}

	/** 
	 * Methods managing event coming from agents 
	 */
	override receiveEvent(^event : Event) {
		if (^event instanceof GuiRepaint) { // event from the environment, GUI must be refreshed
			this.myGUI.drones = scaleDronePosToGui(	^event.perceivedAgentBody) 
			
			var meanDur :float  = 0
			for (d : deliveriesTime){
				meanDur+=d
			}
			meanDur /= deliveriesTime.size
			
			
			this.myGUI.updateDeliveriesMeanDuration(meanDur) 
			this.myGUI.repaint
		}
		if ( ^event instanceof GeneralSepuku){
			stop
		}
		if(^event instanceof ParcelDelivered){
			deliveriesTime.add(^event.deliveredDuration)
		}
	}
}
